@echo off
    setlocal enableextensions

    rem Not to be directly called
    exit /b 9009


:init
	REM # utilities to be invokved by other frontend scripts
	set PROJECT_BASE=%SystemDrive%\projects
	set SENTRY_HOME=%~dp0..
	set SENTRY_LIB=%SENTRY_HOME%\lib
	set SENTRY_CLASSES=%SENTRY_HOME%\classes
	set SENTRY_REPORT_SERVER_URL="http://jenkins_server_missing.ep.com/sentry/"

	REM USERNAME=ml093043
	REM USERPROFILE=C:\Users\ml093043
	REM LOCALAPPDATA=C:\Users\ml093043\AppData\Local
	REM ProgramFiles=C:\Program Files
	REM ProgramFiles(x86)=C:\Program Files (x86)

	if exist "%LOCALAPPDATA%\Google\Chrome\Application\chrome.exe" (
		set DEFAULT_CHROME_BIN="%LOCALAPPDATA%\Google\Chrome\Application\chrome.exe"
	)
	if exist "%ProgramFiles%\Google\Chrome\Application\chrome.exe" (
		set DEFAULT_CHROME_BIN="%ProgramFiles%\Google\Chrome\Application\chrome.exe"
	)
	if exist "%ProgramFiles(x86)%\Google\Chrome\Application\chrome.exe" (
		set DEFAULT_CHROME_BIN="%ProgramFiles(x86)%\Google\Chrome\Application\chrome.exe"
	)

	if exist "%ProgramFiles%\Mozilla Firefox\firefox.exe" (
		set DEFAULT_FIREFOX_BIN="%ProgramFiles%\Mozilla Firefox\firefox.exe"
	)
	if exist "%ProgramFiles(x86)%\Mozilla Firefox\firefox.exe" (
		set DEFAULT_FIREFOX_BIN="%ProgramFiles(x86)%\Mozilla Firefox\firefox.exe"
	)

	REM # setting Java runtissme options and classpath
	set JAVA_OPT=%JAVA_OPT% -Xms256m
	set JAVA_OPT=%JAVA_OPT% -Xmx1024m
	set JAVA_OPT=%JAVA_OPT% -ea
	set JAVA_OPT=%JAVA_OPT% -Dfile.encoding=UTF-8
	set JAVA_OPT=%JAVA_OPT% -Dsentry.home="%SENTRY_HOME%"
	set JAVA_OPT=%JAVA_OPT% -Dwebdriver.winium.verbose=false
	set JAVA_OPT=%JAVA_OPT% -Dwebdriver.winium.silent=false
	set JAVA_OPT=%JAVA_OPT% -Dwebdriver.ie.driver.loglevel=WARN
REM set JAVA_OPT=%JAVA_OPT% -Dwebdriver.winium.logpath=%TEMP%\winium-service.log
REM set JAVA_OPT=%JAVA_OPT% -Dsentry.assistantMode=on

	goto :eof

:title
	echo.
	echo --------------------------------------------------------------------------------
	echo ^|                        sentry - test automation for all                      ^|
	echo --------------------------------------------------------------------------------
	echo [:: %~1
	echo --------------------------------------------------------------------------------
	echo.
	goto :eof



	
:checkJDK8
	for /f "tokens=3" %%g in ('java -version 2^>^&1 ^| findstr /i "version"')do (
    @echo Output: %%g
    set JAVAVER=%%g
)

	set JAVAVER=%JAVAVER:"=%
	@echo Output: %JAVAVER%
	if "%JAVAVER%"=="1.8.0_131" (
		echo java is installed
	) else (
		echo ERROR!!!
		echo JAVA 8 is not installed on your system
		echo.
		exit /b -1
	)
	goto :eof
	
REM # Make sure prerequisite environment variables are set
:checkJava
	if "%JAVA_HOME%"=="" (
		if "%JRE_HOME%"=="" (
			echo ERROR!!!
		    echo Neither the JAVA_HOME nor the JRE_HOME environment variable is defined
		    echo At least one of these environment variables is needed to run this program
		    echo.
		    exit /b -1
		) else (
			if EXIST "%JRE_HOME%\bin\java.exe" (
				set JAVA="%JRE_HOME%\bin\java.exe"
			) else (
				echo ERROR!!!
				echo The JRE_HOME environment variable is not defined correctly.
				echo Unable to find "%JRE_HOME%\bin\java.exe"
				echo.
				exit /b -1
			)
		)
	) else (
		if EXIST "%JAVA_HOME%\bin\java.exe" (
			set JAVA="%JAVA_HOME%\bin\java.exe"
		) else (
			echo ERROR!!!
			echo The JAVA_HOME environment variable is not defined correctly.
			echo Unable to find "%JAVA_HOME%\bin\java.exe"
			echo.
			exit /b -1
		)
	)
	echo setting JAVA as %JAVA%
	goto :eof



:checkOS
	for /f "tokens=1-5 delims=. " %%i in ('ver') do set VERSION=%%i %%j %%k %%l %%m
	goto :eof





:checkRAM
	for /f "tokens=1" %%i in ('wmic MEMORYCHIP get Capacity') do (
		if "%%i"=="Capacity" (
			echo .
		) else (
			set RAM=%%i
			goto :eof
		)
	)
	


:checkInternetSpeed
	for /f "tokens=3,6,9" %%i in ('ping google.com 2^>^&1 ^| findstr /i "maximum"')do (
		set SPEED=%%i.%%j.%%k 
	)
	for /f "delims=., tokens=1-3" %%v in ("%SPEED%") do (
 		set minimum= %%v
	    	set maximum= %%w
	    	set average= %%x
	)
	goto :eof

	

:resolveEnv
	set SENTRY_LIB=%SENTRY_HOME%\lib
	set CLASSES_PATH=%SENTRY_HOME%\classes
	set datestr=%date% %time%
	echo ENVIRONMENT:
	echo   CURRENT TIME:   %datestr%
	echo   CURRENT USER:   %USERNAME%
	echo   CURRENT HOST:   %COMPUTERNAME%
	echo   JAVA:           %JAVA%
	echo   JAVA VERSION:   %JAVAVER%
	echo   OS Version:		%Version%
	echo   RAM:			%RAM%
	echo   SPEED:			%SPEED%
	echo For 32 bytes--- Minimum speed:%minimum% Maximum:%maximum% Average:%average%
	echo   SENTRY_HOME:    %SENTRY_HOME%
	echo   SENTRY_LIB:     %SENTRY_LIB%
	echo   SENTRY_CLASSES: %SENTRY_CLASSES%
	echo   PROJECT_BASE:   %PROJECT_BASE%
	if NOT "%PROJECT_HOME%"=="" (
		echo   PROJECT_HOME:   %PROJECT_HOME%
	)
	goto :eof


:testErrorlevel
    echo testErrorlevel
    exit /b 1
